buildscript {
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.0'
        classpath 'com.google.gms:google-services:1.3.0-beta1'
        classpath 'com.github.triplet.gradle:play-publisher:1.1.2'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
}
apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.triplet.play'

android {
    packagingOptions {
        exclude 'LICENSE.txt'
    }
    lintOptions {
        abortOnError false
    }
    signingConfigs {
        release {
            storeFile rootProject.file("keystore.jks")
            storePassword System.getenv("KEYSTORE_PASS")
            keyAlias System.getenv("ALIAS_NAME")
            keyPassword System.getenv("ALIAS_PASS")
        }
        debug {
            storeFile rootProject.file("keystore.jks")
            storePassword System.getenv("KEYSTORE_PASS")
            keyAlias System.getenv("ALIAS_NAME")
            keyPassword System.getenv("ALIAS_PASS")
        }
    }

    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        def value = 0
        def runTasks = gradle.startParameter.taskNames
        if ('assemble' in runTasks || 'publishRelease' in runTasks || 'assembleRelease' in runTasks || 'aR' in runTasks) {
            value = 1;
        }
        def code = versionProps['VERSION_CODE'] != null ? versionProps['VERSION_CODE'].toInteger() + value : 2
        def name = versionProps['VERSION_NAME'] != null ? versionProps['VERSION_NAME'].toInteger() + value : 1
        versionProps['VERSION_CODE'] = code.toString()
        versionProps['VERSION_NAME'] = name.toString()
        if (value == 1)
            versionProps.store(versionPropsFile.newWriter(), null)
        defaultConfig {
            versionCode code
            versionName "0.0." + name

        }
    }
    play {
        serviceAccountEmail = '92588905880-fdolvkj29p3l637u5fgaa3jke0vksd17@developer.gserviceaccount.com'
        pk12File = file('key.p12')
        //   uploadImages = true
    }

    defaultConfig {
        applicationId "com.tokko.recipesv2"
        minSdkVersion 19
        targetSdkVersion 22
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            // proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            zipAlignEnabled true
        }
        debug {
            signingConfig signingConfigs.release
        }
        realBackend {
            signingConfig signingConfigs.release
            debuggable true
        }
        mock {
            debuggable true
            signingConfig signingConfigs.release
        }
    }
    dexOptions {
        incremental false
        preDexLibraries = false
        jumboMode = false
        javaMaxHeapSize "2048M"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
retrolambda {
    retrolambda {
        include 'Test', 'Debug', 'Release'
        oldJdk = System.getenv("JAVA7_HOME")
        javaVersion = JavaVersion.VERSION_1_7
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.mockito:mockito-all:2.0.2-beta'
    androidTestCompile 'junit:junit:4.12'
    // androidTestCompile 'org.mockito:mockito-all:2.0.2-beta'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
    androidTestCompile('com.android.support.test:runner:0.3') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.3') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    //androidTestCompile 'com.android.support.test:runner:0.3'
    provided 'org.roboguice:roboblender:3.0.1'
    compile 'org.roboguice:roboguice:3.0.1'
    compile 'com.google.android.gms:play-services:7.5.0'
    //compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.annimon:stream:1.0.1'
    // compile 'com.android.support:support-v4:22.2.1'
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(path: ':backend', configuration: 'android-endpoints')
}
